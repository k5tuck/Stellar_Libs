# Get stellarlibs version from include/stellarlibs/version.hpppp and put it in STELLARLIBS_VERSION
function(stellarlibs_extract_version)
    file(READ "${CMAKE_CURRENT_LIST_DIR}/include/stellarlibs/version.hpp" file_contents)
    string(REGEX MATCH "STELLARLIBS_VER_MAJOR ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract major version number from stellarlibs/version.hpp")
    endif()
    set(ver_major ${CMAKE_MATCH_1})

    string(REGEX MATCH "STELLARLIBS_VER_MINOR ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract minor version number from stellarlibs/version.hpp")
    endif()

    set(ver_minor ${CMAKE_MATCH_1})
    string(REGEX MATCH "STELLARLIBS_VER_PATCH ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract patch version number from stellarlibs/version.hpp")
    endif()
    set(ver_patch ${CMAKE_MATCH_1})

    set(STELLARLIBS_VERSION_MAJOR ${ver_major} PARENT_SCOPE)
    set(STELLARLIBS_VERSION_MINOR ${ver_minor} PARENT_SCOPE)
    set(STELLARLIBS_VERSION_PATCH ${ver_patch} PARENT_SCOPE)
    set(STELLARLIBS_VERSION "${ver_major}.${ver_minor}.${ver_patch}" PARENT_SCOPE)
endfunction()